(use 'riemann.hipchat)
(require '[riemann.common :refer [event]])

(let [host "0.0.0.0"]
  (udp-server :host host))

(def librato (librato-metrics librato-email librato-key))

(def hipchat-token)
(def hipchat-room)
(def hc (hipchat {:token hipchat-token
                  :room hipchat-room
                  :from "Bernhard R."
                  :notify 0}))

(periodically-expire 10 {:keep-keys [:host :service :tags]})

(def one-day (* 24 60 60))

(let [the-index (index)
      index (update-index the-index)]
  (streams
   (where (service #"^deploy.*")
          (changed-state
           hc
           (where (state "started")
                  (librato :start-annotation)
                  (else
                   (librato :end-annotation)))))
   (where (tagged-any ["turbovote" "usps-processor" "sendgrid-monitor"])
          (where (or (service "voter-created") (service "voter-completed"))
                 (moving-time-window one-day
                                     (smap (fn [events]
                                             (let [num-created (count (filter #(= "voter-created" (:service %)) events))
                                                   num-completed (count (filter #(= "voter-completed" (:service %)) events))
                                                   fraction (if (zero? num-created) 0
                                                                (/ num-completed num-created))]
                                               (event {:service "voter signup completion rate"
                                                       :metric fraction
                                                       :tags ["turbovote"]})))
                                           (librato :gauge))))
          (librato :gauge))
   (where (tagged "monitor")
          index
          (changed-state
           hc
           (where (state "ok")
                  (librato :start-annotation)
                  (else
                   (librato :end-annotation)))))))
